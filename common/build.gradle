plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "maven-publish"
}

loom {
    accessWidener = file("src/main/resources/cloth-config.accessWidener")
}

configurations {
    shadow
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.architectury.minecraft}"
    mappings minecraft.officialMojangMappings()
    // We depend on fabric loader here to use the fabric @Environment annotations
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

    modApi("me.shedaniel.cloth:basic-math:0.5.1")

    compileOnly("blue.endless:jankson:${rootProject.jankson_version}")
    compileOnly("com.moandjiezana.toml:toml4j:${rootProject.toml4j_version}")
    compileOnly("org.yaml:snakeyaml:${rootProject.snakeyaml_version}")
    shadow("blue.endless:jankson:${rootProject.jankson_version}")
    shadow("com.moandjiezana.toml:toml4j:${rootProject.toml4j_version}") {
        exclude(module: "gson")
    }
    shadow("org.yaml:snakeyaml:${rootProject.snakeyaml_version}")
}

architectury {
    common()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

shadowJar {
    relocate "blue.endless.jankson", "${rootProject.maven_group}.clothconfig.shadowed.blue.endless.jankson"
    relocate "com.moandjiezana.toml", "${rootProject.maven_group}.clothconfig.shadowed.com.moandjiezana.toml"
    relocate "org.yaml.snakeyaml", "${rootProject.maven_group}.clothconfig.shadowed.org.yaml.snakeyaml"

    configurations = [project.configurations.shadow]
    classifier null
}

build {
    dependsOn shadowJar
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = rootProject.archives_base_name
            artifact(file("${project.buildDir}/libs/${project.archivesBaseName}-${project.version}.jar")) {
                builtBy shadowJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}
